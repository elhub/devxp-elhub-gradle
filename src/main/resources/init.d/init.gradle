import com.adarshr.gradle.testlogger.theme.ThemeType
import org.jfrog.gradle.plugin.artifactory.dsl.PublisherConfig
import org.jetbrains.kotlin.gradle.dsl.KotlinCompile

initscript {

    repositories {
        maven {
            url "https://jfrog.elhub.cloud:443/artifactory/elhub-plugins/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
        classpath "com.adarshr:gradle-test-logger-plugin:3.2.0"
        classpath "io.qameta.allure.gradle.allure:allure-plugin:2.10.0"
        classpath "io.qameta.allure.gradle.report:allure-report-plugin:2.10.0"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.29.0"
    }

}

allprojects {
    apply plugin: org.jetbrains.kotlin.gradle.plugin.KotlinPlatformJvmPlugin
    apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin
    apply plugin: 'jacoco'
    apply plugin: com.adarshr.gradle.testlogger.TestLoggerPlugin
    apply plugin: io.qameta.allure.gradle.allure.AllurePlugin
    apply plugin: io.qameta.allure.gradle.download.AllureDownloadPlugin
    apply plugin: org.jfrog.gradle.plugin.artifactory.ArtifactoryPlugin
    apply plugin: 'maven-publish'

    repositories {
        maven {
            url "https://jfrog.elhub.cloud/artifactory/elhub-mvn/"
        }
    }

    /*
     * Compile setup
     */
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    /*
     * Test setup
     */
    test {
        useJUnitPlatform()
        testLogging {
            events("passed", "skipped", "failed")
            showStandardStreams = true
        }
    }

    jacoco {
        toolVersion = '0.8.7' // Has to be the same as TeamCity
    }

    tasks.test {
        finalizedBy(tasks.jacocoTestReport) // report is always generated after tests run
    }

    tasks.jacocoTestReport {
        reports {
            xml.required = true
        }
        dependsOn(tasks.test) // tests are required to run before generating the report
    }

    testlogger {
        theme = ThemeType.MOCHA
    }

    def allureVersion = "2.19.0" // Version of the allure comandline client

    allure {
        version = allureVersion
        autoconfigure = false
        aspectjweaver = true
        useJUnit5 {
            version = allureVersion
        }
    }

    /*
     * Publishing
     */
    artifactoryPublish {
        skip = false //Skip build info analysis and publishing (false by default)
        contextUrl = 'https://jfrog.example.com/artifactory'
        publications('a', 'b', 'c')
        properties = ['qa.level': 'basic', 'q.os': 'win32, deb, osx']
        properties {
            c '**:**:**:*@*', cProperty: 'only in c'
        }
        clientConfig.publisher.repoKey = 'integration-libs'
        clientConfig.publisher.username = 'deployer'
        clientConfig.publisher.password = 'deployerPaS'

        // Minimum file size in KB for which the plugin performs checksum deploy optimization. Default: 10. Set to 0 to disable uploading files with checksum that already exists in Artifactory.
        clientConfig.publisher.minChecksumDeploySizeKb = 10
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    artifactory {
        contextUrl = project.findProperty('artifactoryUri') ?: "https://jfrog.example.com/artifactory"
        publish {
            repository {
                repoKey = project.findProperty('artifactoryRepository') ?: "unknownrepo"
                username = project.findProperty('artifactoryUsername') ?: "nouser"
                password =  project.findProperty('artifactoryPassword') ?: "nopass"
            }
            defaults {
                publications('mavenJava')
                publishBuildInfo = (project.findProperty('artifactoryUsername') == "teamcity")
                publishArtifacts = true
                publishPom = true
            }
        }
        resolve {
            repoKey = "repo"
        }
    }

    tasks["artifactoryPublish"].dependsOn(tasks["assemble"])

    tasks["publish"].dependsOn(tasks["artifactoryPublish"])

    /*
     * TeamCity
     */
    tasks.register('teamCity', Exec) {
        workingDir '.teamcity'
        commandLine 'mvn', 'compile'
        description 'Compile the TeamCity settings'
    }

}
